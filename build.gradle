plugins {
	id "com.crystaelix.loom" version "jitpack-3a42c174"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.7.4"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.21.1"
def modName = "PackagedExCrafting"
def modId = "packagedexcrafting"
def specVersion = "4"
def vendor = "thelm"

group = "thelm.packagedexcrafting"
version = "4.0.0.0"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(21)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "forge.logging.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.24.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.21.1"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.21:2024.07.28@zip"
	}
	neoForge "net.neoforged:neoforge:21.1.72"

	// Dependencies
	implementation "curse.maven:packagedauto-308380:5848087"
	implementation "curse.maven:cucumber-272335:5774468"
	implementation "curse.maven:extended-crafting-268387:5711764"
	implementation("mezz.jei:jei-1.21.1-neoforge:19.21.0.246") {transitive = false}
	implementation "appeng:appliedenergistics2:19.0.23-beta"
	implementation "curse.maven:ae2-jei-integration-1074338:5748513"
	implementation "vazkii.patchouli:Patchouli:1.21-87-NEOFORGE"

	// Testing
	implementation "mekanism:Mekanism:1.21.1-10.7.7.64"
	implementation "curse.maven:applied-mekanistics-574300:5664061"

	//Performance
	implementation "curse.maven:yeetusexperimentus-635427:5444189"
	implementation "curse.maven:modernfix-790626:5659985"
	implementation "curse.maven:ferritecore-429235:5434178"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.addAll("forge", "neoforge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "322861"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "extended-crafting"
		optional "applied-energistics-2"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "qO33ACjS"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "extended-crafting"
		optional "ae2"
	}
}
